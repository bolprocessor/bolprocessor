// Bol Processor version BP2.8.0
// Grammar file saved as '-gr.koto3'. Date: Lun 30 Mars 1998 -- 22:46
-or.koto3
-in.abc1
-tb.koto3
-se.koto3
-ho.abc1

// File Ò-gr.koto3Ó
// A sort of unidimensional cellular automaton producing a
// sort of Turkish music!

// To get the real sound you would need a D50 or D550 synth.
// Use sound patch C-36 "Koto" on the PN-D50-O2 memory card
// or I-21 "Koto" with the D50 dump file enclosed with BP2.

// Here, the vibraphone patch of GeneralMIDI (program #12)
// is automatically selected because the grammar loaded
// the MIDI orchestra file Ò-or.koto3Ó which sent the
// appropriate program change message.

// You may use a MIDI keyboard to control BP2 on channel 15.

// Here comes the grammar

RND
gram#1[1] <100> S --> _vel(127) _volume(40) X X X X Y X X X X Y X X X X
gram#1[2] <K9> X --> a		[Weight is controlled by K9, see Interaction]
gram#1[3] X  --> b
---------------------
SUB
gram#2[1] Y --> Y
gram#2[2] #? ?1 --> #? ?1  [Keep leftmost symbol]
gram#2[3] ?1 #? --> ?1 #?  [Keep rightmost symbol]
gram#2[4] } --> }
gram#2[5] , --> ,
gram#2[6] <5> a --> a
gram#2[7] <30> a b a --> a a
gram#2[8] <100> #({) a b a --> {5,a c b,f f f - f}
gram#2[9] <5> b --> b
gram#2[10] <30> #({) b a b #(}) --> b b
gram#2[11] <100> #({) b a b --> {3,b a b c b a,f a f}
gram#2[12] <100-10> #({) #a b #(}) --> #a c b
gram#2[13] c c c -->  c a c
gram#2[14] c a c --> b a b
gram#2[15] <50> ?1 ?1 ?2 ?3 ?4 ?4 --> ?2 ?3
gram#2[16] <K9> ?1 ?1 ?1 --> ?1
gram#2[17] <K9> ?1 ?1 --> ?1
--------------------
RND
gram#3[1] <40> Y --> <<f>>
gram#3[2] <50> Y --> <<chik>>
gram#3[3] <1-1> ? #? --> ? Silence _script( MIDI send Continue) #? [Apply only once.]
gram#3[4] <50> Silence --> - - - - - 
gram#3[5] <15> Silence --> - - - - 
gram#3[6] <8> Silence --> - - - 
gram#3[7] <8> Silence --> - - 

// That's all folks!