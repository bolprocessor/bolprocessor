// Bol Processor version BP2.9.1
// Grammar file saved as '-gr.dhin--'. Date: Dim 12 Juil 1998 -- 11:31
-se.dhin--
-ho.dhin--

// Example of qa‘ida (grammar for the tabla) transfered from
// Bol Processor BP1
// This is a “true” BP grammar.
// Weights of rules have been inferred from real examples
// You may create your own ‘-mi.dhin--’ using SoundObjectEditor

// Note convention must NOT be English because B3, C6, etc. are used as variables.

RND 
_mm(360.0000) _striated
GRAM#1 [1] <0> S <-> 4+4/6 S96 
GRAM#1 [2] <5>  S <-> 4+4+4+4/6 S192 
---------------- SUBGRAMMAR #2 ------------------------------
RND
GRAM#2 [1] <5>  S192 <-> (= F48 ) (= V24 ) F'24 *(: F48 ) (: V24 ) F24 
GRAM#2 [2] <0> S96 <-> (= V24 ) F'24 *(: V24 ) F24 
GRAM#2 [3] <0>  V24 <-> (= V12 ) (: V12 ) 
GRAM#2 [4] <1>  V24 <-> (= V12 ) *(: V12 )
GRAM#2 [5] <0>  V24 <-> Q24
GRAM#2 [6] <0>  V24 <-> V12 V12
GRAM#2 [7] <4>  V24 <-> B24 
GRAM#2 [8] <0>  V12 <-> (= B6 ) *(: B6 )
GRAM#2 [9] <1>  V12 <-> B12
---------------- SUBGRAMMAR #3 ------------------------------
LIN 
GRAM#3 [1] <2>  B3 <-> A3
GRAM#3 [2] <0>  B4 <-> A4
GRAM#3 [3] <2>  B6 <-> A6
GRAM#3 [4] <0>  B6 <-> C6
GRAM#3 [5] <3>  B24 <-> A3 B21 
GRAM#3 [6] <0>  B24 <-> A4 B20
GRAM#3 [7] <1>  B24 <-> A6 B18
GRAM#3 [8] <0>  B24 <-> C6 B18 
GRAM#3 [9] <3>  B21 <-> A3 B18
GRAM#3 [10] <0>  B21 <-> A6 B15
GRAM#3 [11] <0>  B21 <-> C6 B15
GRAM#3 [12] <0>  B20 <-> A4 B16 
GRAM#3 [13] <0>  B20 <-> C6 B14 
GRAM#3 [14] <3>  B18 <-> A3 B15
GRAM#3 [15] <0>  B18 <-> A4 B14
GRAM#3 [16] <1>  B18 <-> A6 B12
GRAM#3 [17] <0>  B18 <-> C6 B12
GRAM#3 [18] <0>  B16 <-> A4 B12 
GRAM#3 [19] <0>  B16 <-> C6 B10 
GRAM#3 [20] <3>  B15 <-> A3 B12
GRAM#3 [21] <0>  B15 <-> A6 B9
GRAM#3 [22] <0>  B15 <-> C6 B9
GRAM#3 [23] <0>  B14 <-> A4 B10 
GRAM#3 [24] <0>  B14 <-> C6 B8 
GRAM#3 [25] <1>  B12 <-> F'12 
GRAM#3 [26] <1>  B12 <-> A3 B9 
GRAM#3 [27] <0>  A3 B12 <-> A3 A4 B8
GRAM#3 [28] <0>  A6 B12 <-> A6 A4 B8
GRAM#3 [29] <0>  C6 B12 <-> C6 A4 B8
GRAM#3 [30] <0>  ) B12 <-> ) A4 B8 
GRAM#3 [31] <3>  B12 <-> A6 B6 
GRAM#3 [32] <0>  B12 <-> C6 B6
GRAM#3 [33] <0>  B10 <-> A4 B6
GRAM#3 [34] <0>  B10 <-> C6 B4
GRAM#3 [35] <1>  B9 <-> A3 B6
GRAM#3 [36] <0>  B9 <-> A6 B3
GRAM#3 [37] <0>  B9 <-> C6 B3 
GRAM#3 [38] <0>  B8 <-> A4 B4 
GRAM#3 [39] <2>  B6 <-> A3 B3 
-------------------------------------------------------------
RND 
GRAM#4 [1] <4>  A3 <-> dhin--
GRAM#4 [2] <0>  A3 <-> dha--
GRAM#4 [3] <0>  A3 <-> dhagena
GRAM#4 [4] <2>  A3 A3 <-> dhagenadhin--
GRAM#4 [5] <5>  A3 A3 <-> dhagenadha--
GRAM#4 [6] <0>  A4 <-> tirakita
GRAM#4 [7] <0>  A6 <-> dha-dha-dha-
GRAM#4 [8] <0>  A6 <-> dha-ta-dha-
GRAM#4 [9] <0>  A6 <-> dheenedheenedheene
GRAM#4 [10] <1>  A6 <-> dheenedha-dheene
GRAM#4 [11] <5>  A6 <-> dheenedheenagena
GRAM#4 [12] <1>  A6 <-> teeneteenakena
GRAM#4 [13] <0>  A6 <-> dhatigegenaka
GRAM#4 [14] <0>  C6 <-> tagetirakita
-------------------------------------------------------------
RND 
GRAM#5 [1] <1> F'12 <-> dhatigegenakateeneteenakena
GRAM#5 [2] <0>  Q24 <-> dhin--dhagenadha--dhagenadhatigegenakadheenedheenagena
GRAM#5 [3] <5>  F24 <-> tagetirakitadhin--dhagenadhatigegenakadheenedheenagena
GRAM#5 [4] <0>  F24 <-> tagetirakitagena-dhagenadhatigegenakadheenedheenagena
GRAM#5 [5] <5>  F'24 <-> tagetirakitadhin--dhagenadhatigegenakateeneteenakena
GRAM#5 [6] <0>  F'24 <-> tagetirakitagena-dhagenadhatigegenakateeneteenakena
GRAM#5 [7] <5>  F48 <-> dhin--dhagenadha--dhagenadhatigegenakadheenedheenagenatagetirakitadhin--dhagenadhatigegenakateeneteenakena
GRAM#5 [8] <0>  F48 <-> dhin--dhagenadha--dhagenadhatigegenakadheenedheenagenatagetirakitagena-dhagenadhatigegenakateeneteenakena
------------
TEMPLATES:
[1] 4+4*1/6 (@0 (@0 (@0 ______) * (@1 ))(@2 ))________________________ * (@3 )________________________
[2] 4+4*1/6 (@0 (@0 ____________)(@1 ))________________________ * (@2 )________________________
[3] 4+4*1/6 (@0 (@0 (@0 ______) * (@1 )) * (@2 ))________________________ * (@3 )________________________
[4] 4+4*1/6 (@0 (@0 ____________) * (@1 ))________________________ * (@2 )________________________
[5] 4+4*1/6 (@0 ________________________)________________________ * (@1 )________________________
[6] 4+4*1/6 (@0 (@0 ______) * (@1 )(@0 ______) * (@1 ))________________________ * (@3 )________________________
[7] 4+4*1/6 (@0 (@0 ______) * (@1 )____________)________________________ * (@2 )________________________
[8] 4+4*1/6 (@0 ____________(@0 ______) * (@1 ))________________________ * (@2 )________________________
[9] 4+4+4+4*1/6 (@0 ________________________________________________)(@0 (@0 (@0 ______) * (@1 ))(@2 ))________________________ * (@4 )(@3 )________________________
[10] 4+4+4+4*1/6 (@0 ________________________________________________)(@0 (@0 ____________)(@1 ))________________________ * (@3 )(@2 )________________________
[11] 4+4+4+4*1/6 (@0 ________________________________________________)(@0 (@0 (@0 ______) * (@1 )) * (@2 ))________________________ * (@4 )(@3 )________________________
[12] 4+4+4+4*1/6 (@0 ________________________________________________)(@0 (@0 ____________) * (@1 ))________________________ * (@3 )(@2 )________________________
[13] 4+4+4+4*1/6 (@0 ________________________________________________)(@0 ________________________)________________________ * (@2 )(@1 )________________________
[14] 4+4+4+4*1/6 (@0 ________________________________________________)(@0 (@0 ______) * (@1 )(@0 ______) * (@1 ))________________________ * (@4 )(@3 )________________________
[15] 4+4+4+4*1/6 (@0 ________________________________________________)(@0 (@0 ______) * (@1 )____________)________________________ * (@3 )(@2 )________________________
[16] 4+4+4+4*1/6 (@0 ________________________________________________)(@0 ____________(@0 ______) * (@1 ))________________________ * (@3 )(@2 )________________________
------------
