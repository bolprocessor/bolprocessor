// Bol Processor version BP2.8.0
// Grammar file saved as '-gr.Nadaka 1'. Date: Mar 3 Mars 1998 -- 23:18
// This grammar was composed by Nadaka (nadaka@auroville.org.in) in July 1997.

// It is a first attempt, and there are musical mistakes. It is meant to give an
// idea of improvisation in a ÔmodernÕ Carnatic style.
// It normally should be played on a guitar, but QuickTime piano will do for
// a start...

// When Nadaka started designing the grammar he typed notes in abbreviated Indian
// notation: S, R, G, M...  However, there were no octave numbers.  So I wrote
// rules deciding on octaves, using Oc1, Oc2... as variables marking octave
// changes.  I think this method is rather clumsy.  It took time to check the
// rules.  It is better to enter notes from a MIDI keyboard and get them typed
// automatically by BP2, using standard names in the chosen convention.
// (Bernard)

-or.Nadaka
-se.hamsad

// The layout of the different subgrammars is inspired by ÔqaidaÕ pieces
// composed by Indian tabla players.  Of course it is a loose similarity because
// this piece is in a South Indian style whereas tabla is North Indian.

RND
// Structure

// It is clever to append a number to each variable name indicating the duration
// of the musical fragment it represents.  BP2, of course, ignores these numbers.

gram#1[1] Start --> Theme32 Theme32 Bridge32 Theme32 B32 Theme32 V16 V16 V16 V16 Theme32 V16 V16 V16 V16 Theme32 V16 V16 V16 V16 Theme32 V16 V16 V16 V16 Theme16 V16 Theme16 V16  Theme16 V16  Theme16 V16  Theme8 V8 Theme8 V8 Theme8 V8 Theme8 V8 Finale
gram#1[2] Finale --> Theme30 NiSaNiA95 NiSaNiB24 NiSaNiB24 NiSaNiC34 Morai49 Theme32 G _______
--------------------
RND
// Variations

gram#2[1] V16 --> Oc4 G _ G G _ G R R G _ G G _ P G R
gram#2[2] V16 --> Oc4 G R S Oc3 N P N Oc4 S R G P G R S Oc3 N Oc4 S R
gram#2[3] V16 --> Oc4 G __ R S Oc3 N Oc4 S R G __ R S Oc3 N Oc4 S R
gram#2[4] V16 --> Oc4 G _ _ P G R S R G _ _ P G R S R
gram#2[5] V16 --> Oc4 G P N Oc5 S R G P G R S Oc4 N P G R S R

gram#2[6] V8 --> Oc4 G _ G G _ P G R
gram#2[7] V8 --> Oc4 G P G R S Oc3 N Oc4 S R
gram#2[8] V8 --> Oc4 G __ R S Oc3 N Oc4 S R
gram#2[9] V8 --> Oc4 G _ _ P G R S R
gram#2[10] V8 --> Oc4 R S Oc4 N P G R S R
-------------------
RND
// Fixed patterns

gram#3[1] NiSaNiA95 --> NiSaNiA18 P _____________ NiSaNiA18 G _____________ NiSaNiA18 R ____________ 
gram#3[2] Morai49 -->  Oc4 P Oc5 S S S Oc4 P G R S Oc3 N Oc4 S R S Oc3 N P G R S S S S R G P Oc4 S S S R G P N P   S N _ P G R N P _ G R S  P G _ R S R
gram#3[3] NiSaNiA18 --> Oc4 N Oc5 S Oc4 N _ N N _ N N N N _ N N _ N N Oc5 S Oc4
gram#3[4] NiSaNiB24 --> Oc4 P N Oc5 S Oc4 P ____ P N Oc5 S Oc4 G ____ P N Oc5 S Oc4 R ____
gram#3[5] NiSaNiC34 --> Oc4 P N Oc5 S Oc4 P P N Oc5 S Oc4 G P N Oc5 S Oc4 R Oc4 P N Oc5 S Oc4 P P N Oc5 S Oc4 G P N Oc5 S Oc4 R P N Oc5 S Oc4 P N Oc5 S Oc4 G N Oc5 S Oc4 R
gram#3[6] Motto16 --> Oc4 G __ P N Oc5 S Oc4 N P N G _ P G R S Oc3 N

gram#3[7] Theme32 --> Oc4 G __ P N Oc5 S Oc4 N P N G _ P G R S Oc3 N P N __ Oc4 R __ G ____ 4
gram#3[8] Theme30 --> Oc4 G __ P N Oc5 S Oc4 N P N G _ P G R S Oc3 N P N __ Oc4 R __ G ____ 2
gram#3[9] Theme16 --> Oc4 G __ P N Oc5 S Oc4 N P N G _ P G R S Oc3 N
gram#3[10] Theme14 --> Oc4 G __ P N Oc5 S Oc4 N P N G _ P G R
gram#3[11] Theme8 --> Oc4 N G _ P G R S Oc3 N

gram#3[12] Bridge32 --> (=Bridge6)(:Bridge6)
gram#3[13] Bridge6 --> Oc4 G __ P __ N __ Oc5 R __ G _ S _

gram#3[14] B32 --> Oc4 G _ P _ N _ Oc5 R _ G _ Oc4 G P N Oc5 R G S  Oc4 G P N Oc5 R G S  Oc4 G P N Oc5 R G   S R G S R
--------------------
ORD
gram#4[1] Oc3 S --> sa3 Oc3
gram#4[2] Oc3 R --> re3 Oc3
gram#4[3] Oc3 N --> ni3 Oc3
gram#4[4] Oc3 P --> pa3 Oc3
gram#4[5] Oc3 G --> ga3 Oc3
gram#4[6] Oc3 _ --> _ Oc3

gram#4[7] Oc4 S --> sa4 Oc4
gram#4[8] Oc4 R --> re4 Oc4
gram#4[9] Oc4 N --> ni4 Oc4
gram#4[10] Oc4 P --> pa4 Oc4
gram#4[11] Oc4 G --> ga4 Oc4
gram#4[12] Oc4 _ --> _ Oc4

gram#4[13] Oc5 S --> sa5 Oc5
gram#4[14] Oc5 R --> re5 Oc5
gram#4[15] Oc5 N --> ni5 Oc5
gram#4[16] Oc5 P --> pa5 Oc5
gram#4[17] Oc5 G --> ga5 Oc5
gram#4[18] Oc5 _ --> _ Oc5

gram#4[19] Oc3 Oc4 --> Oc4
gram#4[20] Oc4 Oc5 --> Oc5
gram#4[21] Oc3 Oc5 --> Oc5
gram#4[22] Oc5 Oc4 --> Oc4
gram#4[23] Oc4 Oc3 --> Oc3
gram#4[24] Oc5 Oc3 --> Oc3
--------------------
RND
// Here we erase the unused variable to get a clean output.  BP2 would otherwise
// ignore a left-over variable except if it finds a glosssary rule that rewrites
// it.

gram#5[1] Oc3 --> lambda
gram#5[2] Oc4 --> lambda
gram#5[3] Oc5 --> lambda
