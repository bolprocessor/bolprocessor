// Bol Processor version BP2.7.3
// Grammar file saved as '-gr.vina2'. Date: Lun 17 Nov 1997 -- 20:45
-se.Vina
-cs.Vina

// This is the first cycle of the famous Gitam "Sri Gananaada" in raga "Malahari"

RND
gram#1[1] <0> S --> Sri Ga Na Naa Da
gram#1[2] S --> {sa4 _ _,_pitchcont _pitchrange(500) _pitchbend(0) -  _pitchbend(500)} 

gram#1[3] Sri --> {2, StartSlide StartPull sa3 _ Pull0 Slide0 _ Pull2 Slide5 _ Pull0 _ Pull2 _ Pull0 Â
Slide5 _ Slide7 _ _ _ _ _ StopSlide StopPull}

gram#1[4] Ga --> {1, LightGamaka pa3}

gram#1[5] Na --> {1, sa4}

gram#1[6] Naa --> {1, Spurita ni3}

gram#1[7] Da --> {1, LightGamaka sa4}

-----------------
SUB1

// Middle level structures

gram#2[1] LightGamaka ?1 --> StartPull ?1 _ _ Pull0 _ Pull1 _ Pull0 _ _ _ Pull1 StopPull
gram#2[2] Spurita	?1 --> StartSlide ?1 Slide1 _ _ _ _ _ _ _ Slide1 StopSlide

-----------------
SUB1

// Lowest level structures

gram#3[1] Pull0 --> _pitchbend(0)
gram#3[2] Pull1 --> _pitchbend(100)
gram#3[3] Pull2 --> _pitchbend(200)

gram#3[4] Slide0 --> _value(slide, 0)
gram#3[5] Slide1 --> _value(slide, 101)
gram#3[6] Slide5 --> _value(slide, 501)
gram#3[7] Slide7 --> _value(slide, 701)

gram#3[8] StartSlide --> _cont(slide) _value(slide, 0)
gram#3[9] StopSlide --> _fixed(slide)
gram#3[10] StartPull --> _pitchcont _pitchrange(500) _pitchbend(0)
gram#3[11] StopPull --> _pitchfixed
