// Bol Processor version BP2.9.2
// Grammar file saved as '-gr.Visser.Waves'. Date: Mer 9 Sep 1998 -- 21:29
// -gr.Visser.Waves

-se.Visser.Waves

ORD
_mm(60.0000) _striated

gram#1[1] S --> _script(Wait for space) _tempo(0.8) Waves

gram#1[2] Waves --> Frase1 1 Frase1 Frase2 1/2 {_retro _transpose(12) Frase1} {_rotate(2) _transpose(1) Frase2} {_transpose(-13) Frase3} 1/4 {_transpose(-1) Frase1} Frase4 {_retro _transpose(-1) Frase1} {_rotate(3) _transpose(-11) Frase4} 1 Frase1 {_keyxpand(C4,-1) Frase1} 1/2 {2, _keyxpand(B3,-1) _vel(40) M19, M24} 1/2 {2, _keyxpand(A#3,-1) _vel(40) M19, M24} 1/4 Frase5 {_keyxpand(B3,-1)  _transpose(-1) Frase5} {_keyxpand(C4,-1) Frase1} 1/2 {2, _keyxpand(B3,-1) _vel(40)  M19, M24} 1/2 {2, _keyxpand(A#3,-1) _vel(40) M19, M24} 1 Frase6 1 Frase6 F1 B3 1 {_vel(50) F1 B3} 2 {6, _vel(40)  F1}

gram#1[3] Frase1 --> _legato(100) {_velcont _vel(50) M1 M2 M3  M4 _vel(60) M5 M6 _vel(50)}
gram#1[4] Frase2 --> _legato(100) {_velcont _vel(50) _transpose(2) M7 M8 _vel(60) M9 M10 M11 M12 _vel(50)}
gram#1[5] Frase3 --> _legato(100) {_velcont _vel(50) _transpose(2) M13 M14 M15 _vel(60) M16 M17 M18 _vel(50)}
gram#1[6] Frase4 --> _legato(100) {_velcont _vel(50) _transpose(2) M19 M20 M21 M22 _vel(60) M23 M24 _vel(50)}
gram#1[7] Frase5 --> _legato(100) {_velcont _vel(50) _transpose(2) M25 _vel(60) M26 M27 M28 M29 M30 _vel(50)}
gram#1[8] Frase6 --> _legato(100) {_velcont _vel(50) _transpose(2) M31 _vel(60) M32 M33 M34 M35 M36 M37 M38 _vel(50)}

gram#1[9] M1 --> {5,  C3 F#3
gram#1[10] M2 --> {3, _transpose(13) C3 F#3
gram#1[11] M3 --> {3, _transpose(1) C3 F#3
gram#1[12] M4 --> F2 B3}
gram#1[13] M5 --> F2 B3}
gram#1[14] M6 --> F1 B3}

gram#1[15] M7 --> {5, _vel(60) C3 F#3
gram#1[16] M8 --> {3, _transpose(13) C3 F#3
gram#1[17] M9 --> {3, _transpose(1) C3 {M8 M10} F#3
gram#1[18] M10 --> F2 B3}
gram#1[19] M11 --> F2 B3}
gram#1[20] M12 --> F1 B3}

gram#1[21] M13 --> {5, _vel(60) C3 F#3
gram#1[22] M14 --> {5, _transpose(13) C3 {_transpose(1) M13 M17} F#3
gram#1[23] M15 --> {5, _transpose(1) C3 {M14 M16} F#3
gram#1[24] M16 --> F2 B3}
gram#1[25] M17 --> F2 B3}
gram#1[26] M18 --> F4 B3}

gram#1[27] M19 --> {9, C3 F#3
gram#1[28] M20 --> {9, _transpose(13) C3 {_transpose(1) M13 M17} F#3
gram#1[29] M21 --> {9, _transpose(1) C3 {M14 M16} F#3
gram#1[30] M22 --> F2 {18, _transpose(-11) M1 M2 M3 M4 M5 M6} B3}
gram#1[31] M23 --> F2 B3}
gram#1[32] M24 --> F1 B3}

gram#1[33] M25 --> {15, _vel(60) C3 F#3
gram#1[34] M26 --> {15, _transpose(13) C3 {_transpose(1) M1 M5} F#3
gram#1[35] M27 --> {15, _transpose(1) C3 {M2 M4} F#3
gram#1[36] M28 --> F2 {30, _transpose(-11) M1 {30, _transpose(-11) M1 M5 M1 M5 M3 M6} M2 M3 M4 M5 M6} B3}
gram#1[37] M29 --> F2 B3}
gram#1[38] M30 --> F1 B3}

gram#1[39] M31 --> {6,  C3 F#3
gram#1[40] M32 --> {5, _transpose(13) C3 F#3
gram#1[41] M33 --> {5, _transpose(1) C3 F#3
gram#1[42] M34 --> {5, _transpose(1) C3 F#3
gram#1[43] M35 --> F2 B3}
gram#1[44] M36 --> F2 B3}
gram#1[45] M37 --> F1 B3}
gram#1[46] M38 --> F1 B3}

COMMENT:
Received:    9/09/98 17:15
From:        hvisser, hvisser@wxs.nl

Again a 'serious' piece for piano solo. It is build on the idea of
imbedding: {5, A B {3, A B {2, A B C D} C D} C D}.
You can get very rubato-rhythms at this way. The title is (until now)
'waves' (of the sea). It “s a bit an 'impression', in the sense of a
reference to the music of Debussy.